apply plugin: 'kotlin-multiplatform'
apply plugin: 'kotlinx-serialization'
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "org.jetbrains.kotlin.mpp_app_android"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }

    packagingOptions {
        exclude 'META-INF/*.kotlin_module'
    }
}

kotlin {
    targets {
        fromPreset(presets.android, 'android')

        // This preset is for iPhone emulator
        // Switch here to presets.iosArm64 (or iosArm32) to build library for iPhone device
        fromPreset(presets.iosX64, 'ios') {
            compilations.main.outputKinds('FRAMEWORK')
        }
    }
    sourceSets {
        configure([androidMain]) {
            dependsOn commonMain
        }
        configure([iosMain]) {
            dependsOn commonMain
        }
//        configure([ios64Main]) {
//            dependsOn iosMain
//        }
    }
}

dependencies {
    commonMainImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:${rootProject.ext.coroutinesVersion}"
    commonMainImplementation "io.ktor:ktor-client-core:${rootProject.ext.ktorVersion}"
    commonMainImplementation "io.ktor:ktor-client-json:${rootProject.ext.ktorVersion}"
    commonMainImplementation "org.kodein.di:kodein-di-core:${rootProject.ext.kodeinVersion}"
    commonMainImplementation "org.kodein.di:kodein-di-erased:${rootProject.ext.kodeinVersion}"

    androidMainImplementation fileTree(dir: "libs", include: ["*.jar"])
    androidMainImplementation 'androidx.appcompat:appcompat:1.0.2'
    androidMainImplementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    androidMainImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.2.1"
    androidMainImplementation "io.ktor:ktor-client-android:${rootProject.ext.ktorVersion}"
    androidMainImplementation "io.ktor:ktor-client-json-jvm:${rootProject.ext.ktorVersion}"
    androidMainImplementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:${rootProject.ext.serializationVersion}"

    androidMainImplementation "com.github.bumptech.glide:glide:${rootProject.ext.glideVersion}"
    annotationProcessor "com.github.bumptech.glide:compiler:${rootProject.ext.glideVersion}"

    iosMainImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-native:${rootProject.ext.coroutinesVersion}"
    iosMainImplementation "io.ktor:ktor-client-ios:${rootProject.ext.ktorVersion}"
    iosMainImplementation "io.ktor:ktor-client-json-native:${rootProject.ext.ktorVersion}"
    iosMainImplementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-native:${rootProject.ext.serializationVersion}"

    commonTestImplementation "org.jetbrains.kotlin:kotlin-test"
    commonTestImplementation "org.jetbrains.kotlin:kotlin-test-junit"

    androidTestImplementation 'androidx.test:runner:1.1.1'
}

// This task attaches native framework built from ios module to Xcode project
// (see iosApp directory). Don't run this task directly,
// Xcode runs this task itself during its build process.
// Before opening the project from iosApp directory in Xcode,
// make sure all Gradle infrastructure exists (gradle.wrapper, gradlew).
task copyFramework {
    def buildType = project.findProperty("kotlin.build.type") ?: "DEBUG"
    def target = project.findProperty("kotlin.target") ?: "ios"
    dependsOn "link${buildType.toLowerCase().capitalize()}Framework${target.capitalize()}"

    doLast {
        def srcFile = kotlin.targets."$target".compilations.main.getBinary("FRAMEWORK", buildType)
        def targetDir = getProperty("configuration.build.dir")
        copy {
            from srcFile.parent
            into targetDir
            include 'app.framework/**'
            include 'app.framework.dSYM'
        }
    }
}
